public with sharing class RestructureTxnEditController extends TransactionControllerBase{

    public Transaction_Processing_Queue__c mRestructureTxn {get; set;}
    public List<QueuedTransactionParamObject> mTxnParamObjectList {get; set;}
    
    private TransactionSetupObject mTxnSetupObject;
    private Boolean mIsRedirect;
    private PageReference mRedirectPage;
    private Map<String, RecordType> mTxnQueueRecordTypeMap; //move to base class
    
    public Boolean mIsPreviewed {get; private set;}
    
    public Lending_Contract__c mRestructuredContract {get; private set;}
    public List<Contract_Repayment__c> mRestructuredRepayments {get; private set;}
    
    public RestructureTxnEditController(ApexPages.StandardController pStdController){
        super();
        mRestructureTxn = (Transaction_Processing_Queue__c)pStdController.getRecord();
        init();
    }
    
    private void init(){
        if (mRestructureTxn.Id != null){
            mRestructureTxn = (Transaction_Processing_Queue__c) fsCore.SObjectQueryUtil.getSObjectRecord(mRestructureTxn.Id);
        }
        
        Map<String,String> pageURLParamMap = ApexPages.currentPage().getParameters();
        mTxnParamObjectList = new List<QueuedTransactionParamObject>();
        mIsRedirect = false;
        mIsPreviewed = false;
        
        mRestructuredContract = new Lending_Contract__c();
        mRestructuredRepayments = new List<Contract_Repayment__c>();
        
        //get contract Id
        Id contractId = null;
        if (pageURLParamMap.containsKey('parentId')){
            contractId = Id.valueOf(pageURLParamMap.get('parentId'));
        }
        else if (mRestructureTxn.Lending_Contract_Number__c != null){
            contractId = mRestructureTxn.Lending_Contract_Number__c;
        } 
        else {
            throw new ServicingException(Label.Parent_Contract_Id_Missing);
        }

        setLendingContract(LendingContractUtil.getLendingContract(contractId));
        setContractStages(getLendingContract().Id);
        setContractAssignments(getLendingContract().Id);
        setBusinessDate(getLendingContract().Branch_Code__c);
        setPageTitle(getLendingContract().Name);
        
        //get transaction code
        String txnCode = Constants.BLANK_STRING;
        if (pageURLParamMap.containsKey('txnCode')){
            txnCode = pageURLParamMap.get('txnCode');
        } 
        else if (mRestructureTxn.Id != null && String.IsNotBlank(mRestructureTxn.Transaction_Code__c)){
            txnCode = mRestructureTxn.Transaction_Code__c;
        }
        
        //redirect to select transaction screen if transaction code is not found
        if (String.IsBlank(txnCode)) {
            mIsRedirect = true;
            mRedirectPage = getNewTransactionPage();
            mRedirectPage.getParameters().put('Id', contractId);
        }
        else {
            setTransactionCategory(Constants.TRANSACTION_CATEGORY_RESTRUCTURE);
            setAvailableTransactions();
            if (!isTransactionAccessible(txnCode)){
                throw new ServicingException(Label.Transaction_Not_Accessible);
            }
            
            mTxnSetupObject = getTransactionObject(mTxnCode);

            if (mRestructureTxn.Id == null){
                setPageSubtitle(Label.Restructure_contract_title);
            } else {
                //TO DO : move to base class
                //populate record type map
                mTxnQueueRecordTypeMap = fsCore.RecordTypeUtil.getInstance()
                                                    .getRecordTypeMapByName(Schema.SObjectType.Transaction_Processing_Queue__c.getName());
                
                //cannot edit processed transactions
                if (mRestructureTxn.RecordTypeId == mTxnQueueRecordTypeMap.get(Constants.RECORD_TYPE_PROCESSED).Id){
                    throw new ServicingException(Label.Processed_Txn_Edit_Not_Allowed);
                }
                
                setPageSubtitle(Label.Restructure_contract_title + ' (#' + mRestructureTxn.Name + ')');
            }
            
            setRestructureTxnDetails();
        }
    }        

    public PageReference onLoad(){
        if (mIsRedirect && mRedirectPage != null){
            mRedirectPage.setRedirect(true);
            return mRedirectPage;
        }
        return null;
    }
    
    private void setRestructureTxnDetails(){
        if (mRestructureTxn.Id == null){
            mRestructureTxn = new Transaction_Processing_Queue__c();
            mRestructureTxn.Lending_Contract_Number__c = getLendingContract().Id;
            mRestructureTxn.Transaction_Date__c = getBusinessDate();
            mRestructureTxn.Transaction_Name__c = mTxnSetupObject.mTransactionId;
            mRestructureTxn.Transaction_Operation_Type__c = mTxnSetupObject.mOperationType;
            mRestructureTxn.Processing_User__c = UserInfo.getUserId();
        }
        
        mTxnParamObjectList = QueuedTransactionParamUtil.getParameterObjectList(mRestructureTxn, mTxnSetupObject);
    }
    
    public PageReference onPreview(){
        mHasError = false;
        validateTransaction();
        
        if (mHasError){ return null; }
        
        TransactionProcessingObject tpObject = new TransactionProcessingObject();
        tpObject.mTransactionQueueRecord = mRestructureTxn;
        tpObject.mTransactionParameterList = new List<Transaction_Processing_Parameter__c>();
        for (QueuedTransactionParamObject txnParamObj : mTxnParamObjectList){
            mTransactionParameterList.add(txnParamObj.getTxnParam());
        }
        tpObject.mProcessingUserId = UserInfo.getUserId();
        tpObject.mProcessingUserProfileId = UserInfo.getProfileId();
        tpObject.mIsTransactionPosting = false;
        tpObject.mLendingContractId = getLendingContract().Id;
        tpObject.mLendingContractNumber = getLendingContract().Name;
        tpObject.mTransactionDate = mRestructureTxn.Transaction_Date__c;
        tpObject.mTransactionSetupId = mTxnSetupObject.mTransactionId;
        tpObject.mNewTransactionRecord = new Contract_Transaction__c();
        
        List<TransactionProcessingObject> tpResultObj = TransactionProcessingGateway.processTransactions(
                                                            new List<TransactionProcessingObject>{tpObject}, true);
        
        if (!tpResultObj.get(0).mIsSuccess){
            mHasError = true;
            for(String errMsg : tpResultObj.get(0).mErrorStack){
                addErrorToPage(errMsg);
            }
        }

        if (!mHasError){
            mRestructuredContract = processRslt.get(0).mResultDataObject.mLendingContractMap.get(tpObject.mLendingContractId);
            mRestructuredRepayments = processRslt.get(0).mResultDataObject.mContractRepaymentMap.get(tpObject.mLendingContractId);
            mIsPreviewed = true;
        }        
        
        return null;
    }

    private void validateTransaction(){
        if (mRestructureTxn.Transaction_Date__c == null){
            mHasError = true;
            addErrorToPage(Label.Transaction_Processing_Date_Blank);
        }
        
        if (mRestructureTxn.Processing_User__c == null){
            mHasError = true;
            addErrorToPage(Label.Transaction_Processing_User_Blank);
        }

        for(QueuedTransactionParamObject txnParamObj : mTxnParamObjectList){
            if(QueuedTransactionParamUtil.isRequiredParamBlank(txnParamObj)){
            mHasError = true;
                addErrorToPage(String.format(Label.Transaction_Processing_Required_Parameter_Blank, new String[]{txnParamObj.getTxnParam().Name}));
            }
        }
    }
    
    private void saveTransaction(String pTxnQueueStatus){
        mHasError = false;
        validateTransaction();
            
        if (mHasError){ return; }
        
        mRestructureTxn.Processing_Status__c = pTxnQueueStatus;
        mRestructureTxn.Processing_Result__c = Constants.BLANK_STRING;

        fsCore.DMLResult txnQueueSaveRslt = QueuedTransactionDMLWrapper.getInstance().upsertData(new List<Transaction_Processing_Queue__c>{mRestructureTxn});
        if (txnQueueSaveRslt.hasErrors()){
            mHasError = true;
            addErrorsToPage(txnQueueSaveRslt.getErrorList());
        }
        
        if (mHasError){ return; }

        List<Transaction_Processing_Parameter__c> queuedTxnParamList = new List<Transaction_Processing_Parameter__c>();
        for(QueuedTransactionParamObject txnParamObj : mTxnParamObjectList){
            Transaction_Processing_Parameter__c txnParam = txnParamObj.getTxnParam();
            if (txnParam.Transaction_Queue_Name__c == null){
                txnParam.Transaction_Queue_Name__c = mRestructureTxn.Id;
            }
            queuedTxnParamList.add(txnParam);
        }                 

        fsCore.DMLResult txnParamSaveRslt = QueuedTransactionParamDMLWrapper.getInstance().upsertData(queuedTxnParamList);
        if (txnParamSaveRslt.hasErrors()){
            mHasError = true;
            addErrorsToPage(txnParamSaveRslt.getErrorList());
        }
    }

    private void postTransaction(){
        TransactionProcessingObjectBuilder tpObject = new TransactionProcessingObjectBuilder();
        tpObject.addQueuedTransaction(mRestructureTxn.Id);
        tpObject.build();
        
        List<TransactionProcessingObject> tpResultObj = TransactionProcessingGateway.processTransactions(tpObject.getTransactionProcessingObjectList(),false);
        
        if (!tpResultObj.get(0).mIsSuccess){
            mHasError = true;
            for(String errMsg : tpResultObj.get(0).mErrorStack){
                addErrorToPage(errMsg);
            }
        }
    }
    
    public PageReference onSaveAsDraft(){
        Savepoint sp = Database.setSavepoint();
        saveTransaction(Constants.TRANSACTION_QUEUE_STATUS_DRAFT);

        if (mHasError){
            Database.rollback(sp);
            return null;
        }

        return getRecordPage(mRestructureTxn.Id);
    }

    public PageReference onPostTxn(){
        if (!mIsPreviewed){
            addErrorToPage(Label.Transaction_Preview_Before_Save_Or_Post);
            return null;
        }

        Savepoint sp = Database.setSavepoint();
        saveTransaction(Constants.TRANSACTION_QUEUE_STATUS_PROCESSING);
    
        if (!mHasError){
            postTransaction();
        }

        if (mHasError){
            Database.rollback(sp);
            return null;
        }

        return getContractRecordPage();
    }

    public PageReference onCancel(){
        return (mRestructureTxn.Id == null ? getContractRecordPage() : getRecordPage(mRestructureTxn.Id));
    }
    
}
